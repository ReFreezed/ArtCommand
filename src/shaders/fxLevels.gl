/***************************************************************
*
*  Effect: Levels
*
*---------------------------------------------------------------
*
*  Art Command
*  by Marcus 'ReFreezed' ThunstrÃ¶m
*
***************************************************************/

#include "_srgb"
#include "_colorSpaces"

uniform vec2 rangeIn;  // {min, max}
uniform vec2 rangeOut; // {min, max}

uniform float middle;

#define H x
#define S y
#define P z

vec4 effect(vec4 loveColor, Image tex, vec2 texUv, vec2 screenPos) {
	vec4 sample  = Texel(tex, texUv);
	sample.rgb  /= sample.a;
	if (srgbMode)  sample.rgb = pow(sample.rgb, vec3(1/2.2));

	vec3 hsp    = toHsp(sample.rgb);
	float power = pow(10, middle*2-1);

	float rangeInDelta = rangeIn.y - rangeIn.x;
	rangeInDelta       = (rangeInDelta >= 0) ? max(rangeInDelta, 1e-6) : min(rangeInDelta, -1e-6);

	hsp.P = max((hsp.P-rangeIn.x)/rangeInDelta, 0); // Normalize from old range.
	hsp.P = pow(hsp.P, power); // Apply curve.
	hsp.P = mix(rangeOut.x, rangeOut.y, hsp.P); // Map to new range.

	vec3 result = fromHsp(hsp);
	if (srgbMode)  result.rgb = pow(result.rgb, vec3(2.2));
	return vec4(clamp(result, vec3(0), vec3(1))*sample.a, sample.a);
}
