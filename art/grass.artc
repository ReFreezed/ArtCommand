#
# "Grass" by ReFreezed, 2022-09-13
#
W = 5 #half
H = 40

Dx = 10
Dy = 6

Bend = 7

canvas size200 aa8

# Grass.
fill .12 .32 .12

func Straw X Y
	H         = (H * randf(.7, 1))
	BendAngle = (1.5 * Tau/4 * randf(-1, 1))

	point Bend*(cos(BendAngle)-1) -H
	for 1 10  1; point-clear (lerp(0,  W, I/10) + Bend*(cos(lerp(BendAngle, 0, I/10))-1)) (lerp(-H, 0, I/10)); end
	for 10 1 -1; point-clear (lerp(0, -W, I/10) + Bend*(cos(lerp(BendAngle, 0, I/10))-1)) (lerp(-H, 0, I/10)); end

	grad .6 1 .6, .1 .3 .1 rot90deg
	poly X Y
end

for Y -Dy CanvasH+H+Dy Dy
	for X -Dx CanvasW+Dx Dx
		X = X+Dx*randf(-.45,.45)
		Y = Y+Dy*randf(-.45,.45)
		Straw X Y
	end
end

# Post-processing.
grad rgb0 .15, rgb0 .02, rgb0 0 rot90deg
rect 0 0 CanvasW CanvasH

gamma .6
brightness 1.15
saturation 1.1
