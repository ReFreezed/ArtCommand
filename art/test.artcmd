# Parsing tests.
setFoo value"a\'b\"c\nw\\e"; setFoo+5,Foo'd
setFoo (1 --[[)]] + (false and ")))\n"..[[)\]] or 0) - --[=[((]=] 1)

setFoo +value; setFoo value True ; setFoo valueTrue ; setFoo value(true )
setFoo -value; setFoo value False; setFoo valueFalse; setFoo value(false)

func TestBlobs X Y Size
	set Col -1
	func TestBlobColumn R G B
		setx Col (Col+1)
		color R G B 1  ; circle (X+(Col+.5)*Size) (Y+(0+.5)*Size) r(Size/2)
		color R G B .75; circle (X+(Col+.5)*Size) (Y+(1+.5)*Size) r(Size/2)
		color R G B .50; circle (X+(Col+.5)*Size) (Y+(2+.5)*Size) r(Size/2)
		color R G B .25; circle (X+(Col+.5)*Size) (Y+(3+.5)*Size) r(Size/2)
	end
	TestBlobColumn 1 1 1, 0 0 0, 1 0 0
end



# Settings.
canvas 500 600 aa4
backdrop0 .3 0 .5
# backdrop (40/255) (40/255) (40/255)
fill.4 .4 .4 .5
# zoom 2

# Draw stuff.
color 1 1 1 .1
font 62  # Big boy!
text 495 -5 ax1 kx-.15 wrap550 lineh.9 align"right" "TEST TEST TEST TEST

color 0 0 0
circle 50 50 r10

color 1 0 .5 .7
circle 190 110 30 10
circle 190 110 12 25
color 1 0 .5
circle 260 110 30 10 "line" thick2
circle 260 110 12 25 "line" thick2

font 'resources/SquadaOne.ttf' 19
color 1 1 .7
text 100 18 -filter 'Good? "GOOD"?! Good.'

imagefont "resources/ReFreezedSmall.fnt"
color .8 1 1
text 100 38 -filter "Yippee ki-yay!"

imagefont "resources/abc.png" spacing1 "ABC"
color 1 1 1 .5
text 170 39 -filter "ABCBA"

# Transform.
move 50 50

push
	rotate 20deg
	color 1 1 1
	rect 0 0 10 10 ax.5 ay.5

	set X 16
	circle -X 0 3
pop

# Masks.
makemask
	rect w20 h20
	rect w30 h50 y35
	circle 50 50 r10

	font 24
	text text"Hello, world!" x12 -filter

	TestBlobs 1 65 4
	TestBlobs 1 90 4

	grad rgb1 1, rgb0 1; rect 24 (40+0*14) 12 10
	grad rgb1 1, rgb1 0; rect 24 (40+1*14) 12 10
	grad rgb0 1, rgb0 0; rect 24 (40+2*14) 12 10

mask
	color 1 50% 1; rect w1000 h1000 anchor.5
	TestBlobs 1 1 4
	color; font14; text "YES" x36 y42
mask False

TestBlobs -13 1 4

# Line shapes.
color 1 1 0
circle "line" r24, "line" x22 y-15 r10

# Reset transform.
origin
color 1 .6 .5
circle r15

# Functions.
func DrawCircle X Y
	func DrawCircle X Y #inner
		circle X Y r10
	end
	DrawCircle (2*X) Y #inner
end

set "RG" .7
color RG RG 1
set  Tens "foo"
setx Tens (2*15)
DrawCircle y170 x80
DrawCircle(50+25) (220 - Tens)

# Loops.
color .5 0 0 .5
for 1 7 2
	rect (200+I*10) 160 10 50
end

# Blocks.
color 0 0 0
set X 0
do
	set X 100
end
circle "line" thick4 X 250 r10

# Gradients.
push
	move 320 90
	grad 0 1 1, 0 0 1 1               ; rect (0*40) 0 30 60
	grad 0 1 1, 0 0 1 1 45deg scale2.5; rect (1*40) 0 30 60
	grad 0 1 1, 0 0 1 1 -75deg        ; rect (2*40) 0 30 60 "line" thick4

	move 0 85
	grad 0 1 1, 0 0 1 1               ; circle x(15+0*40) r15
	grad 0 1 1, 0 0 1 1 45deg scale2.5; circle x(15+1*40) r15
	grad 0 1 1, 0 0 1 1 -75deg        ; circle x(15+2*40) r15 "line" thick4
pop

push
	setR1 .5; setG1 .2; setB1 .2
	setR2 .7; setG2 .7; setB2 1

	move 360 210
	color R1 G1 B1; rect 0 0 120 160

	move 10 10
	color R2 G2 B2; rect 0 0 100 110

	move 30 0
	grad R1 G1 B1, R2 G2 B2 1 45deg  ; rect 0  0  30 30
	grad R1 G1 B1, R2 G2 B2 1 135deg ; rect 40 0  30 30
	grad R1 G1 B1, R2 G2 B2 1 -135deg; rect 40 40 30 70
	grad R1 G1 B1, R2 G2 B2 1 -45deg ; rect 0  40 30 70
	move -30 0

	grad R1 G1 B1, R2 G2 B2 1 180deg; rect 0  0  10 50
	grad R1 G1 B1, R2 G2 B2 1 0deg  ; rect 10 0  10 50

	move 0 60
	grad R1 G1 B1, R2 G2 B2 1 -90deg; rect 0  0  20 25
	grad R1 G1 B1, R2 G2 B2 1 90deg ; rect 0  25 20 25
	move 0 -60

	move 0 120
	color R2 G2 B2; rect 0 0 100 20
	grad R1 G1 B1, R2 G2 B2 1 0deg ; circle 0  0 r10   anchor0
	grad R1 G1 B1, R2 G2 B2 1 90deg; circle 20 0 r10   anchor0
	grad R1 G1 B1, R2 G2 B2 1 45deg; circle 40 0 r10   anchor0
	grad R1 G1 B1, R2 G2 B2 1 45deg; circle 60 0 20 10 anchor0
pop

push
	move 170 330
	grad rgb0,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,rgb0,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,rgb0 scale.5
	rect 0 0 60 10
	grad rgb0,,,,,,,,,,,,,1 .3 .3,1 .3 .3,1 .3 .3,1 .3 .3,,,,,,,,,,,,,rgb0 +radial
	rect 0 20 60 10, 0 40 60 30, 70 0 30 70
	circle 110 35 10 30 ax0, 140 35 20 10 ax0

	move 0 80
	grad 1 0 0, 1 1 0, 0 1 0, 0 1 1, 0 0 1, 1 0 1, 1 0 0 1 45deg
	rect 0 0 60 20
	grad 1 0 0, 1 1 0, 0 1 0, 0 1 1, 0 0 1, 1 0 1, 1 0 0 1 +radial -fit
	rect 0 30 60 50
	circle 70 0 r40 anchor0
pop

# State.
color 0 0 1
rect 20 (300+0*15) 30 10
push
	grad .5 0 0, 0 1 0
	rect 20 (300+1*15) 30 10
pop
rect 20 (300+2*15) 30 10

push
	move 60 300; color 1 .2 .2; rect
	push
		move 0 10; color 0 .9 0; rect
		makemask
			move 0 10; circle r5 anchor0
			push
				move 0 10; circle r5 anchor0
			pop
			move 10 0; circle r5 anchor0
		mask
			fill 1 1 1
		maskFalse
	pop
	move 10 0; color 0 0 1; rect
pop

# Images.
color rgb1           ; image "face.png" 100 250 sx.5
grad 1 .4 .4, .4 1 .4; image "face.png" 155 270 sx.3 sy.2

color rgb1 .13
image "face.png" 150 (round(.7*500)) ax1 ay.5 scale3.5 -filter

set Circles "circles.artcmd"
color rgb1  1                 ; imageCircles (185+0*50) 230
color rgb1 .5                 ; imageCircles (185+1*50) 230
grad a.5,a1,a0,a1,a.5 rot90deg; imageCircles (185+2*50) 230

# Layers.
setlayer"foo" 40 40
	makemask
		fill 1 1 1
		grey0; rect xy15 size10
	mask
		grey1
		rect 15 0 10 40
		rect 0 15 40 10
	maskFalse
color 0 1 1 ; layer"foo" 40 190 anchor.5 rot45deg
color 0 1 .5; layer"foo" 80 190 anchor.5 rot55deg

# Debug.
assert (1 < 5)
print Circles, (3^7), (rep("Hey",3))

# Lines and polygons.
set O -40  # Offset.
point xyO, (70+O)(20+O), (30+O)(50+O), (40+O)(80+O)
color 0 1 .5 1 ; line (70-O)(395-O) thick4
color 0 1 .5 .5; line (70-O)(395-O) thick4 rot20deg
color 0 1 .5 .2; line (70-O)(395-O) thick4 rot20deg -shift

set O -10  # Offset.
point xyO, (20+O)O, (40+O)(15+O), xy(40+O), (15+O)(40+O), O(20+O)
color 1 .7 .8 1 ; poly "fill" (30-O)(435-O)
color 1 .7 .8 .5; poly "fill" (30-O)(435-O) sx3 rot15deg
color 1 .7 .8 .2; poly "fill" (30-O)(435-O) sx3 rot40deg -shift

point 0 -15, 15 0, 0 35, -15 0
grad 1 .3 0, 1 1 .4 1  0deg; poly "fill" 35 550
grad 1 .3 0, 1 1 .4 1 90deg; poly "fill" 75 550
grad 1 .3 0, 1 1 .4 1  0deg; poly "line" 115 550 thick4
grad 1 .3 0, 1 1 .4 1 90deg; poly "line" 155 550 thick4

# Arcs.
func Arc X Y Mode1 Mode2 From To Segs
	circle Mode1 X (Y   ) r15 fromFrom toTo segsSegs thick4
	circle Mode2 X (Y+38) r15 fromFrom toTo segsSegs thick4
end

grad .3 .5 1, rgb1 +radial
Arc (195+0*33) 520 "fill"       "line"       0deg   999deg 8
Arc (195+1*33) 520 "fill"       "line"       -90deg 135deg 5
Arc (195+2*33) 520 "fill"       "linepie"    -90deg 135deg 5
Arc (195+3*33) 520 "fillclosed" "lineclosed" -90deg 135deg 5
Arc (195+4*33) 520 "fill"       "linepie"    90deg  135deg 3

# Rounded rectangles.
grad .9 .4 1, .9 .1 1 1 45deg
rect"fill" (340+0*50) (390+0*50) size40 thick4 segs2
rect"fill" (340+0*50) (390+1*50) size40 thick4 segs2 r10
rect"fill" (340+0*50) (390+2*50) size40 thick4 segs2 r999
rect"fill" (340+0*50) (390+3*50) size40 thick4 segs2 rx17 ry9
rect"line" (340+1*50) (390+0*50) size40 thick4 segs2
rect"line" (340+1*50) (390+1*50) size40 thick4 segs2 r10
rect"line" (340+1*50) (390+2*50) size40 thick4 segs2 r999
rect"line" (340+1*50) (390+3*50) size40 thick4 segs2 rx999 ry10

# Effect: blur.
setlayer"fx"
	grey0; rect (440   ) (390+0*50   ) 40 40
	grey1; rect (440+10) (390+0*50+10) 20 20
	boxblur xy10
layer"fx"
setlayer"fx"
	grey0; rect (440   ) (390+1*50   ) 40 40
	grey1; rect (440+10) (390+1*50+10) 20 20
	blur xy30
layer"fx"

# Effects: color modifications.
# contrast .3
# brightness .5
# saturation .3
# gamma 2.2
# overlay 1 0 1 .6

# tint 0 0 1 1    # Fully saturated blue.
# tint 0 1 1 .5   # Half towards fully saturated cyan.
# tint 1 0 .5 .5  # Half towards fully saturated red-purple.
# tint 0 0 0 1    # No saturation.
# tint 1 1 1 1    # No saturation.

# tint 0 0 .5 1   # 50% saturated blue. (Between black[000] and blue[001].)
# tint .5 .5 1 1  # 50% saturated blue. (Between blue[001] and white[111].)

# tint 0 .5 0 1   # 50% saturated green. (Between black[000] and green[010].)
# tint .5 1 .5 1  # 50% saturated green. (Between green[010] and white[111].)

# Generator: noise.
makemask; rect 440 (390+2*50) 40 40 r10; mask; color;             noise scale.2
makemask; rect 440 (390+3*50) 40 40 r10; mask; grad 1 0 0, 0 0 1; noise z21.5 scale.15
maskFalse
